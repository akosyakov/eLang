grammar ch.vorburger.el.EL with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/xbase/Xbase"
import "platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.ecore" as types
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate eL "http://www.vorburger.ch/el/EL"

XExpression returns XExpression :
	XOrExpression;

XPrimaryExpression returns XExpression:
	XLiteral |
	XIfExpression |
	XParenthesizedExpression;

XLiteral returns XExpression:
  XBooleanLiteral |
  XNullLiteral |
  XStringLiteral |
  DecimalLiteral |
  DateLiteral |
  DateTimeLiteral;

DateLiteral:
	{DateLiteral} value=DATE;	

DateTimeLiteral:
	{DateTimeLiteral} value=DATE_TIME;	

DecimalLiteral:
  {DecimalLiteral} value=NUMBER;

XNullLiteral returns XExpression :
	{XNullLiteral} ('null' | 'NULL');
	
XBooleanLiteral returns XExpression :
	{XBooleanLiteral} (('false' | 'FALSE') | isTrue?=('true' | 'TRUE'));	
	
terminal DATE_TIME:
	(('0'..'9')('0'..'9')'.'('0'..'9')('0'..'9')'.'('0'..'9')('0'..'9')' '('0'..'9')('0'..'9')':'('0'..'9')('0'..'9')':'('0'..'9')('0'..'9'))|(('0'..'9')('0'..'9')'.'('0'..'9')('0'..'9')'.'('0'..'9')('0'..'9')('0'..'9')('0'..'9')' '('0'..'9')('0'..'9')':'('0'..'9')('0'..'9')':'('0'..'9')('0'..'9'))|(('0'..'9')('0'..'9')'/'('0'..'9')('0'..'9')'/'('0'..'9')('0'..'9')' '('0'..'9')('0'..'9')':'('0'..'9')('0'..'9')':'('0'..'9')('0'..'9'))|(('0'..'9')('0'..'9')'/'('0'..'9')('0'..'9')'/'('0'..'9')('0'..'9')('0'..'9')('0'..'9')' '('0'..'9')('0'..'9')':'('0'..'9')('0'..'9')':'('0'..'9')('0'..'9'));

terminal DATE:
	(('0'..'9')('0'..'9')'.'('0'..'9')('0'..'9')'.'('0'..'9')('0'..'9'))|(('0'..'9')('0'..'9')'.'('0'..'9')('0'..'9')'.'('0'..'9')('0'..'9')('0'..'9')('0'..'9'))|(('0'..'9')('0'..'9')'/'('0'..'9')('0'..'9')'/'('0'..'9')('0'..'9'))|(('0'..'9')('0'..'9')'/'('0'..'9')('0'..'9')'/'('0'..'9')('0'..'9')('0'..'9')('0'..'9'));

terminal NUMBER: 
	((('0'..'9')+'.'('0'..'9')*(('e'|'E'|'d'|'D')('-'|'+')?('0'..'9')+)?)|('.'('0'..'9')+(('e'|'E'|'d'|'D')('-'|'+')?('0'..'9')+)?)|(('0'..'9')+(('e'|'E'|'d'|'D')('-'|'+')?('0'..'9')+)?));
  